apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse'

tasks.withType(JavaCompile) {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    options.encoding = 'UTF-8'
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    providedCompile 'javax:javaee-api:7.0'
    testCompile 'fish.payara.extras:payara-micro:4.1.153'
}

//HEADコミットのハッシュ値を書き出すタスク。
//warに含めておいてどのコミットから作成されたものか分かるようにしたい。
task writeHeadCommitHash << {
    def proc = 'git rev-parse @'.execute()
    def file = new File(processResources.destinationDir, 'head')
    file.parentFile.mkdirs()
    file.write(proc.text.trim())
}

war.dependsOn writeHeadCommitHash

//Dockerでjava-youというタグを付けてビルドするタスク。
task dockerBuild(type:Exec) {
    commandLine 'docker', 'build', '-t', 'java-you'
}

dockerBuild.dependsOn war

//Payara Microで動かすタスク。
task run(type:Exec) {
    def payaraJar = configurations.testCompile.find { it.name ==~ /payara-micro.*/ }
    def warFile = war.archivePath
    commandLine 'java', '-jar', "$payaraJar", '--deploy', "$warFile"
}

run.dependsOn war
