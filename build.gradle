apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse'
apply plugin: 'idea'

sourceCompatibility = 1.8
targetCompatibility = 1.8

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    providedCompile 'javax:javaee-api:7.0'
    testCompile 'fish.payara.extras:payara-micro:4.1.153'
}

task writeHeadCommitHash << {
    def proc = 'git rev-parse @'.execute()
    def file = new File(processResources.destinationDir, 'head')
    file.parentFile.mkdirs()
    file.write(proc.text.trim())
}

war.dependsOn writeHeadCommitHash

task dockerBuild << {
    def proc = "docker build -t java-you .".execute()
    proc.waitForProcessOutput(System.out, System.err)
}

dockerBuild.dependsOn war

task run << {
    def payaraJar = configurations.testCompile.find { it.name ==~ /payara-micro.*/ }
    def javaYouWar = war.archivePath
    def proc = "java -jar $payaraJar --deploy $javaYouWar".execute()
    Runtime.runtime.addShutdownHook(new Thread({ -> proc.destroy() } as Runnable))
    proc.waitForProcessOutput(System.out, System.err)
}

run.dependsOn war
